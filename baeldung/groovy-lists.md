# Groovy Lists

## 1. Обзор

В [Groovy](https://www.baeldung.com/groovy-language) мы можем работать со списками так же, как в [Java](https://www.baeldung.com/java-collections). Но благодаря поддержке методов расширения он предлагает гораздо больше.

В этом уроке мы рассмотрим подход Groovy к изменению, фильтрации и сортировке списков.

## 2. Создание списков Groovy

Groovy предоставляет некоторые интересные возможности для работы с коллекциями, используя поддержку динамической типизации и буквального синтаксиса.

Начнем с создания списка с некоторыми значениями, используя сокращенный синтаксис:

```groovy
def list = [1,2,3]
```

Аналогично мы можем создать пустой список:

```groovy
def emptyList = []
```

По умолчанию Groovy создает экземпляр java.util.ArrayList.

Однако **мы также можем указать тип создаваемого списка**:

```groovy
def linkedList = [1,2,3] as LinkedList
ArrayList arrList = [1,2,3]
```

Затем списки можно использовать для создания других списков с помощью аргумента конструктора:

```groovy
def copyList = new ArrayList(arrList)
```

Мы также можем сделать это путем клонирования:

```groovy
def cloneList = arrList.clone()
```

Обратите внимание, что клонирование создает неполную копию списка.

Groovy использует оператор `"=="` для сравнения элементов в двух списках на предмет равенства.

Продолжая предыдущий пример, при сравнении cloneList с arrList результат верен - true:

```groovy
assertTrue(cloneList == arrList)
```

Теперь давайте посмотрим, как выполнять некоторые распространенные операции со списками.

## 3. Получение элементов из списка

Мы можем получить элемент из списка, используя буквальный синтаксис:

```groovy
def list = ["Hello", "World"]
assertTrue(list[1] == "World")
```

Или мы можем использовать методы get() и getAt():

```groovy
assertTrue(list.get(1) == "World")
assertTrue(list.getAt(1) == "World")
```

Мы также можем получить элементы из списка, используя как положительные, так и отрицательные индексы.

При использовании отрицательного индекса список читается справа налево:

```groovy
assertTrue(list[-1] == "World")
assertTrue(list.getAt(-2) == "Hello")
```

Обратите внимание, что метод get() не поддерживает отрицательные индексы.

## 4. Добавление элементов в список

Существует несколько сокращенных способов добавления элементов в список.

Давайте определим пустой список и добавим в него несколько элементов:

```groovy
def list = []

list << 1
list.add("Apple")
assertTrue(list == [1, "Apple"])
```

Далее мы также можем указать индекс для размещения элемента.

Кроме того, **если длина списка меньше указанного индекса, Groovy добавляет нулевые значения null с учетом разницы между ними**:

```groovy
list[2] = "Box"
list[4] = true
assertTrue(list == [1, "Apple", "Box", null, true])
```

Наконец, мы можем использовать оператор `"+="` для добавления новых элементов в список.

По сравнению с другими подходами, **этот оператор создает новый объект списка и присваивает его переменной list**:

```groovy
def list2 = [1,2]
list += list2
list += 12        
assertTrue(list == [1, 6.0, "Apple", "Box", null, true, 1, 2, 12])
```

## 5. Обновление элементов в списке

Мы можем обновлять элементы в списке, используя литеральный синтаксис или метод set():

```groovy
def list =[1, "Apple", 80, "App"]
list[1] = "Box"
list.set(2,90)
assertTrue(list == [1, "Box", 90,  "App"])
```

В этом примере элементы с индексами 1 и 2 обновляются новыми значениями.

## 6. Удаление элементов из списка

Мы можем удалить элемент по определенному индексу, используя метод remove():

```groovy
def list = [1,2,3,4,5,5,6,6,7]
list.remove(3)
assertTrue(list == [1,2,3,5,5,6,6,7])
```

Мы также можем удалить элемент, используя метод removeElement().

Это удаляет первое вхождение элемента из списка:

```groovy
list.removeElement(5)
assertTrue(list == [1,2,3,5,6,6,7])
```

Кроме того, мы **можем использовать оператор minus, чтобы удалить все вхождения элемента из списка**.

Однако этот оператор не изменяет базовый список — он возвращает новый список:

```groovy
assertTrue(list - 6 == [1,2,3,5,7])
```

## 7. Итерация по списку

Groovy добавил новые методы к существующему API коллекций Java.

Эти методы упрощают такие операции, как фильтрация, поиск, сортировка, агрегирование и т. д. за счет инкапсуляции шаблонного кода. Они также поддерживают широкий спектр входных данных, включая замыкания и структуры выходных данных.

Начнем с рассмотрения двух методов перебора списка.

Метод each() допускает замыкание и очень похож на метод foreach() в Java.

Groovy передает неявный параметр it, соответствующий текущему элементу на каждой итерации:

```groovy
def list = [1,"App",3,4]
list.each {println it * 2}
```

Другой метод, eachWithIndex(), предоставляет текущее значение индекса в дополнение к текущему элементу:

```groovy
list.eachWithIndex{ it, i -> println "$i : $it" }
```

## 8. Фильтрация

Фильтрация — еще одна операция, которая часто выполняется над списками, и Groovy предоставляет на выбор множество различных методов.

Давайте определим список для работы:

```groovy
def filterList = [2,1,3,4,5,6,76]
```

Чтобы найти первый объект, соответствующий условию, мы можем использовать find:

```groovy
assertTrue(filterList.find {it > 3} == 4)
```

Чтобы найти все объекты, соответствующие условию, мы можем использовать findAll:

```groovy
assertTrue(filterList.findAll {it > 3} == [4,5,6,76])
```

Давайте посмотрим на другой пример.

Здесь нам нужен список всех элементов, которые являются числами:

```groovy
assertTrue(filterList.findAll {it instanceof Number} == [2,1,3,4,5,6,76])
```

В качестве альтернативы мы можем использовать метод grep, чтобы сделать то же самое:

```groovy
assertTrue(filterList.grep( Number ) == [2,1,3,4,5,6,76])
```

**Разница между методами grep и find заключается в том, что grep может принимать в качестве аргумента объект Object или замыкание Closure.**

Таким образом, это позволяет еще больше сократить оператор условия до минимума:

```groovy
assertTrue(filterList.grep {it > 6} == [76])
```

Кроме того, grep использует Object#isCase(java.lang.Object) для оценки состояния каждого элемента списка.

Иногда нас **могут интересовать только уникальные элементы списка**. Для этой цели мы можем использовать два перегруженных метода.

Метод unique() опционально принимает замыкание и сохраняет в базовом списке только те элементы, которые соответствуют условиям замыкания, отбрасывая остальные.

По умолчанию для определения уникальности используется естественный порядок:

```groovy
def uniqueList = [1,3,3,4]
uniqueList.unique()
assertTrue(uniqueList == [1,3,4])
```

В качестве альтернативы, если не требуется изменять базовый список, мы можем использовать метод toUnique():

```groovy
assertTrue(["A", "B", "Ba", "Bat", "Cat"].toUnique {it.size()} == ["A", "Ba", "Bat"])
```

**Если мы хотим проверить, что некоторые или все элементы списка удовлетворяют определенному условию, мы можем использовать методы every() и any().**

Метод every() оценивает условие замыкания для каждого элемента в списке.

Затем он возвращает true только в том случае, если все элементы в списке удовлетворяют условию:

```groovy
def conditionList = [2,1,3,4,5,6,76]
assertFalse(conditionList.every {it < 6})
```

С другой стороны, метод any() возвращает true, если хотя бы какой-либо элемент в списке удовлетворяет условию:

```groovy
assertTrue(conditionList.any {it % 2 == 0})
```

## 9. Сортировка

По умолчанию Groovy сортирует элементы списка в соответствии с их естественным порядком:

```groovy
assertTrue([1,2,1,0].sort() == [0,1,1,2])
```

Но мы также можем передать компаратор Comparator с собственной логикой сортировки:

```groovy
Comparator mc = {a,b -> a == b? 0: a < b? 1 : -1}
def list = [1,2,1,0]
list.sort(mc)
assertTrue(list == [2,1,1,0])
```

Кроме того, мы можем использовать методы min() или max(), чтобы найти максимальное или минимальное значение без явного вызова sort():

```groovy
def strList = ["na", "ppp", "as"]
assertTrue(strList.max() == "ppp")
```

```groovy
Comparator minc = {a,b -> a == b? 0: a < b? -1 : 1}
def numberList = [3, 2, 0, 7]
assertTrue(numberList.min(minc) == 0)
```

## 10. Сборка коллекции

Иногда нам может потребоваться изменить элементы в списке и вернуть другой список с обновленными значениями.

Мы можем сделать это, используя метод collect():

```groovy
def list = ["Kay","Henry","Justin","Tom"]
assertTrue(list.collect{"Hi " + it} == ["Hi Kay","Hi Henry","Hi Justin","Hi Tom"])
```

## 11. Объединение

Иногда нам может потребоваться объединить элементы в списке.

Для этого мы можем использовать метод join():

```groovy
assertTrue(["One","Two","Three"].join(",") == "One,Two,Three")
```

## 12. Заключение

В этой статье мы рассмотрели несколько расширений, которые Groovy добавляет к API коллекций Java.

Мы начали с рассмотрения буквального синтаксиса, а затем его использования при создании, обновлении, удалении и извлечении элементов в списке.

Наконец, мы рассмотрели поддержку Groovy для итерации, фильтрации, поиска, сбора, объединения и сортировки списков.

Как всегда, все примеры, обсуждаемые в статье, [доступны на GitHub](https://github.com/eugenp/tutorials/tree/master/core-groovy-modules/core-groovy-collections).
